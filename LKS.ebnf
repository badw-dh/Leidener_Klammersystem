# LKS-grammar

### Infos and Ressources ##############################################
# Unicode character search: https://www.amp-what.com/unicode/search/
# Unicode Explorer: https://unicode-explorer.com/ , https://unicode.mayastudios.com/
#######################################################################


#######################################################################
#
#  EBNF-Directives
#
#######################################################################

@ whitespace  = / */
@ literalws   = none
# @ comment     = /#.*/
@ ignorecase  = False
@ reduction   = merge
@ drop        = whitespace, backticked

#######################################################################
#
# Grammar for the Leiden-System used in The German Inscriptions
#
#######################################################################

inscription = (inline | special | LF )+ §EOF
HIDE:inline = phrases | letters | partial | unreadable | separator | space | footnote | tag
HIDE:tag    = opening_tag | closing_tag
opening_tag =  /<(appnum|appalpha)[^>]*>[^<]*<\/1>/+
closing_tag =  /<\/?[^>]+>/+
letters     = /[A-Za-z0-9]/+
phrases     = (letters | partial) "."
partial     = /[ạḅ]/+   # TODO: hier noch alle Buchstaben mit Punkt auflisten
unreadable  = `.`+ | `+`+ # TODO: Umbauen, muss in Klammern sein
# punctuation = `/`
space       = " "~ ! separator
separator   = ~"∙"~
footnote    = /[a-z]\)/

HIDE:special = rasure | vacat | missing | restored | omission | litura | redundancy
rasure      = `[[` § inline+ `]]`
vacat       = `[vacat]`
missing     = `[` (unreadable | unknown) `]`
restored    = `[` § (inline | LF) `]`
omission    = `(` § (letters | space )+ `)`
litura      = `<` § correct (`=` false)? `>` # TODO: named entities instead of < >. TODO: See DI rules
correct     = { letters | restored | space }
false       = inline
redundancy  = `{` § inline+ `}`
letter      = /[a-z]/
unknown     = `-`+

LF          = /[\n|]|\|\|/ | ~"/"~
DROP:EOF =  !/./