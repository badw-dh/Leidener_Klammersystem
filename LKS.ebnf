# LKS-grammar

### Infos and Ressources ##############################################
# Unicode character search: https://www.amp-what.com/unicode/search/
# Unicode Explorer: https://unicode-explorer.com/ , https://unicode.mayastudios.com/
#######################################################################


#######################################################################
#
#  EBNF-Directives
#
#######################################################################

@ whitespace  = linefeed
@ literalws   = right
# @ comment     = /#.*/
@ ignorecase  = False
@ reduction   = merge
@ drop        = whitespace, backticked

#######################################################################
#
# Grammar for the Leiden-System (and some variants)
#
#######################################################################

inscription = (inline | special )+ §EOF
HIDE:inline = letters | partial | unreadable | punctuation | escape | space | footnote
letters     = /[a-z](?!\))/+
partial     = /[ạḅ]/+   # TODO: hier noch alle Buchstaben mit Punkt auflisten
unreadable  = `.`+ | `+`+
punctuation = `/`
space       = /[ ∙]/
footnote    = /[a-z]\)/
escape      = /\\./

HIDE:special = rasure | vacat | missing | restored | omission | litura | redundancy
rasure      = `[[` § inline+ `]]`
vacat       = `[vacat]`
missing     = `[` unreadable | unknown `]`
restored    = `[` § (inline | unreadable | unknown) `]`
omission    = `(` § (letters|letter|space|escape)+ `)`
litura      = `<` § correct (`=` false)? `>`
correct     = letters
false       = letters
redundancy  = `{` § inline+ `}`
letter      = /[a-z]/
unknown     = `-`+

LF          = /\n|\||\|\|/
DROP:EOF =  !/./
