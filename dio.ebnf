# DIO-SCO-grammar

### Infos and Ressources ##############################################
# Unicode character search: https://www.amp-what.com/unicode/search/
# Unicode Explorer: https://unicode-explorer.com/ , https://unicode.mayastudios.com/
#######################################################################


#######################################################################
#
#  EBNF-Directives
#
#######################################################################

@ whitespace  = / */
@ literalws   = none
# @ comment     = /#.*/
@ ignorecase  = False
@ reduction   = merge
@ drop        = whitespace, backticked

#######################################################################
#
# Grammar for the Content of the inscription field in the DIO-T3 database
#
#######################################################################

sco            = prettyspace "<sco>" prettyspace sec+ "</sco>" §EOF
sec            = sec_one | sec_multi
HIDE:sec_one   = "<sec>" prettyspace par "</sec>" prettyspace
HIDE:sec_multi = "<sec>" prettyspace sn prettyspace par "</sec>" prettyspace
HIDE:sn        = (( snt | snr ) prettyspace) +
snr            = "<snr>" /[A-Z]+/ "." prettyspace "</snr>"
snt            = "<snt>" prettyspace inline+ prettyspace "</snt>"
par            = "<par>" prettyspace (lno | lin | table)+ "</par>" prettyspace

lno            = "<lno>" § inscription "</lno>" prettyspace
lin            = "<lin>" § inscription "</lin>" prettyspace

table  = "<table>" prettyspace ( row prettyspace )+ "</table>"
row    = "<row>" prettyspace ( cell prettyspace )+ "</row>"
cell   = "<cell>" prettyspace ( entry prettyspace )+ "</cell>"
entry  = "<entry>" prettyspace § inscription "</entry>"

HIDE:inscription = (inline | special | prettyspace )+
HIDE:inline = phrases | token | space

phrases           = (token | special)+ phrase_terminator
phrase_terminator = "." | ":" | ","

HIDE:token = tags | insec | letters | unreadable | separator
HIDE:tags   = apptag | ligtag | breaktag | btag | strongtag | emtag
apptag      =  /<(appnum|appalpha)[^>]*>[^<]*<\/\1>/+
ligtag      = "<lig>" | "</lig>"
breaktag    = "<nl></nl>"
btag       = "<b>" | "</b>"
strongtag  = "<strong>" | "</strong>"
emtag       = "<em>" | "</em>"

letters = letters_plain | letters_extended | cross
HIDE:letters_plain       = /[A-Za-z0-9](?!\u0323|&#x323;)/+
HIDE:letters_extended    = /[àèìòùÀÈÌÒÙáéíóúýÁÉÍÓÚÝâêîôûÂÊÎÔÛãñõÃÑÕäëïöüÿÄËÏÖÜŸçÇßØøÅåÆæœ](?!\u0323)/+
cross       = /[+†]/

insec            =  combined | precomposed
HIDE:combined    = /[a-zA-Z0-9](\u0323|&#x323;)/
HIDE:precomposed = /[ẠḄḌẸḤỊḴḶṂṆỌṚṢṬỤṾẈỴẒạḅḍẹḥịḳḷṃṇọṛṣṭụṿẉỵẓ]/

separator   = separator_word_insec | separator_word |  separator_word_dot | separator_syl  | separator_line
separator_word       = ~"∙"~ | ~"·"~
separator_word_dot       = ~"."~
separator_word_insec = ~ /[∙·] ?\u0323/ ~
separator_line       = ~"/"~

separator_syl = separator_syl_double_insec | separator_syl_double | separator_syl_single | separator_syl_space | separator_syl_nextline
separator_syl_double_insec  = /=\u0323\/=|=\/=\u0323/
separator_syl_double        = ~"=/="~
separator_syl_single        = ~"=/"~
separator_syl_space        = ~"= /"~
separator_syl_nextline        = ~"/="

HIDE:special = rasure | deletion | cpl | omission | addendum | supo
rasure       = `[[` § inline+ `]]`
deletion     = `[` (unreadable | unknown | threedots) `]`
cpl          = `[` § (inline | unknown | tags | omission)+ `]`
omission     = `(` § (letters | insec | space )+ `)`
addendum     = `&lt;` (tags | special | unknown | inline)+ `&gt;`
supo    = "°"

threedots        = "…"
unreadable  = `.`+
unknown     = `---`

HIDE:prettyspace  = /[\r\n ]*/
space             = " "~ ! separator

DROP:EOF    =  !/./

