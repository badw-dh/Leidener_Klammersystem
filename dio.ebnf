# DIO-SCO-grammar

### Infos and Ressources ##############################################
# Unicode character search: https://www.amp-what.com/unicode/search/
# Unicode Explorer: https://unicode-explorer.com/ , https://unicode.mayastudios.com/
#######################################################################


#######################################################################
#
#  EBNF-Directives
#
#######################################################################

@ whitespace  = / */
@ literalws   = none
# @ comment     = /#.*/
@ ignorecase  = False
@ reduction   = merge
@ drop        = whitespace, backticked

#######################################################################
#
# Grammar for the Content of the inscription field in the DIO-T3 database
#
#######################################################################

sco            = prettyspace "<sco>" prettyspace sec+ "</sco>" §EOF
sec            = sec_one | sec_multi
HIDE:sec_one   = "<sec>" prettyspace par "</sec>" prettyspace
HIDE:sec_multi = "<sec>" prettyspace snr prettyspace par "</sec>" prettyspace
snr            = "<snr>" /[A-Z]+/ "." "</snr>"
par            = "<par>" prettyspace lno+ "</par>" prettyspace
lno            = "<lno>" § inscription "</lno>" prettyspace

HIDE: inscription = (tags | inline | special | prettyspace )+
HIDE:inline = phrases | insec | letters | unreadable | separator | space

HIDE:tags   = apptag | ligtag
apptag      =  /<(appnum|appalpha)[^>]*>[^<]*<\/\1>/+
ligtag      = ("<lig>" | "</lig>")

letters = letters_plain | letters_extended | cross
HIDE:letters_plain       = /[A-Za-z0-9](?!\u0323)/+
HIDE:letters_extended    = /[àèìòùÀÈÌÒÙáéíóúýÁÉÍÓÚÝâêîôûÂÊÎÔÛãñõÃÑÕäëïöüÿÄËÏÖÜŸçÇßØøÅåÆæœ](?!\u0323)/+

cross       = /[+†]/

insec            =  combined | precomposed
HIDE:combined    = /[a-zA-Z0-9]\u0323/
HIDE:precomposed = /[ẠḄḌẸḤỊḴḶṂṆỌṚṢṬỤṾẈỴẒạḅḍẹḥịḳḷṃṇọṛṣṭụṿẉỵẓ]/

separator   = separator_word_insec | separator_word |  separator_syl  | separator_line
separator_word       = ~"∙"~ | ~"·"~
separator_word_insec = ~ /[∙·] ?\u0323/ ~
separator_line       = ~"/"~

separator_syl = separator_syl_double_insec | separator_syl_double | separator_syl_single
separator_syl_double_insec  = /=\u0323\/=|=\/=\u0323/
separator_syl_double        = ~"=/="~
separator_syl_single        = ~"=/"~

phrases           = (letters | insec) phrase_terminator
phrase_terminator = "." | ":"

HIDE:prettyspace  = /[\r\n ]*/
space             = " "~ ! separator

HIDE:special = rasure | deletion | cpl | omission | addendum
rasure       = `[[` § inline+ `]]`
deletion     = `[` (unreadable | unknown | threedots) `]`
cpl          = `[` § (inline | unknown | tags | omission)+ `]`
omission     = `(` § (letters | insec | space )+ `)`
addendum     = `&lt;` (tags | special | unknown | inline)+ `&gt;`

threedots        = "…"
unreadable  = `.`+
unknown     = `---`

DROP:EOF    =  !/./

