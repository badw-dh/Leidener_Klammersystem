# LKS-grammar

### Infos and Ressources ##############################################
# Unicode character search: https://www.amp-what.com/unicode/search/
# Unicode Explorer: https://unicode-explorer.com/ , https://unicode.mayastudios.com/
#######################################################################


#######################################################################
#
#  EBNF-Directives
#
#######################################################################

@ whitespace  = / */
@ literalws   = none
# @ comment     = /#.*/
@ ignorecase  = False
@ reduction   = merge
@ drop        = whitespace, backticked

#######################################################################
#
# Grammar for the Leiden-System (and some variants)
#
#######################################################################

inscription = (inline | special | LF )+ §EOF
HIDE:inline = letters | partial | unreadable | escape | separator | space | footnote
letters     = /[A-Za-z0-9](?!\))/+
partial     = /[ạḅ]/+   # TODO: hier noch alle Buchstaben mit Punkt auflisten
unreadable  = `.`+ | `+`+
# punctuation = `/`
space       = " "~ ! separator
separator   = ~"∙"~
footnote    = /[a-z]\)/
escape      = /\\./

HIDE:special = rasure | vacat | missing | restored | omission | litura | redundancy
rasure      = `[[` § inline+ `]]`
vacat       = `[vacat]`
missing     = `[` unreadable | unknown `]`
restored    = `[` § (inline | LF) `]`
omission    = `(` § (letters|letter|space|escape)+ `)`
litura      = `<` § correct (`=` false)? `>`
correct     = { letters | restored | space }
false       = inline
redundancy  = `{` § inline+ `}`
letter      = /[a-z]/
unknown     = `-`+

LF          = /[\n|]|\|\|/ | ~"/"~
DROP:EOF =  !/./
